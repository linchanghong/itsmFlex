<?xml version="1.0" encoding="utf-8"?>
<s:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" 
		  width="100%" height="100%"
		  initialize="module1_initializeHandler(event)"
		  implements="com.itsm.common.as3.IModules"
		  creationComplete="module1_creationCompleteHandler(event)">
	
	
	<fx:Script>
		<![CDATA[
			import com.adobe.serializers.json.JSONDecoder;
			import com.adobe.serializers.json.JSONEncoder;
			import com.itsm.common.mxml.AddMenuButtonLeaf;
			import com.itsm.common.mxml.AddOrgLeaf;
			import com.itsm.common.renders.MenuTreeItemRender;
			import com.itsm.common.utils.AppCore;
			import com.framelib.utils.StaticMethods;
			
			import common.utils.TAlert;
			
			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.core.IFlexDisplayObject;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.ResultEvent;
			
			import spark.components.Group;
			
			[Bindable]
			public var appCore:AppCore = AppCore.getInstance();
			[Bindable]
			private var orgData:ArrayCollection;
			private var theParent:Object = new Object();
			
			private var windowObj:Object = new Object();
			
			public function setWindowObj(obj:Object):void
			{
				this.windowObj = obj;
			}
			
			protected function module1_initializeHandler(event:FlexEvent):void
			{
				StaticMethods.creatButtons(this, appCore.userModuleButton);
				
				if(appCore.orgData != null && appCore.orgData.length != 0) {
					orgData = appCore.orgData;
				} else {
					findOrgTreeData(appCore.loginUser.companyId);
				}
			}
			
			private function findOrgTreeData(orgId:int):void
			{
				appCore.dataDeal.dataRemote("frameAPI","FrameAPI","findOrgTreeByOrgId",[orgId.toString()]);
				appCore.dataDeal.remoteMethods.addEventListener(ResultEvent.RESULT, findOrgTreeByOrgIdHandler);
			}
			
			private function findOrgTreeByOrgIdHandler(event:ResultEvent):void
			{
				if(event.result != null) {
					orgData = new JSONDecoder().decode(event.result as String) as ArrayCollection;
					appCore.orgData = orgData;
				} else {
					TAlert.show("查找数据失败","提示");
				}
			}
			
			protected function module1_creationCompleteHandler(event:FlexEvent):void
			{
				orgTree.addEventListener("AddLeafEvent", addLeafEventHandler);
				orgTree.addEventListener("EditLeafEvent", editLeafEventHandler);
				orgTree.addEventListener("DeleteLeafEvent", deleteLeafEventHandler);
			}
			
			/**
			 * 删除机构
			 * */
			protected function deleteLeafEventHandler(event:MouseEvent):void
			{
				if(orgTree.selectedItem.children != null) {
					TAlert.show("删除机构 "+ orgTree.selectedItem.orgName +" ？对应的子机构也将一并删除！", "提示", TAlert.NO|TAlert.YES, this, deleteParentOrg);
				} else {
					appCore.dataDeal.dataRemote("frameAPI","FrameAPI","deleteOrgById",[orgTree.selectedItem.id.toString()]);
					appCore.dataDeal.remoteMethods.addEventListener(ResultEvent.RESULT, deleteOrgByIdHandler);
				}
			}
			
			private function deleteParentOrg(event:CloseEvent):void
			{
				if (event.detail == TAlert.YES) {
					var ids:Array = getSeleteItemIds(orgTree.selectedItem);
					
					appCore.dataDeal.dataRemote("frameAPI","FrameAPI","deleteOrgByIds",[new JSONEncoder().encode(ids)]);
					appCore.dataDeal.remoteMethods.addEventListener(ResultEvent.RESULT, deleteOrgByIdHandler);
				}
			}
			
			private function getSeleteItemIds(item:Object):Array
			{
				var ids:Array = new Array();
				ids.push(item.id);
				if(item.children != null) {
					var childArr:ArrayCollection = item.children;
					for each(var item:Object in childArr) {
						ids = ids.concat(getSeleteItemIds(item));
					}
				}
				return ids;
			}
			
			private function deleteOrgByIdHandler(event:ResultEvent):void
			{
				if(event.result != null && event.result.toString() != "0") {
					var children1:ArrayCollection = (orgTree.getParentItem(orgTree.selectedItem) as Object).children as ArrayCollection;
					for(var i:Number=0; i < children1.length; i++) {
						if( children1[i].id == orgTree.selectedItem.id ) {
							children1.removeItemAt(i);
							break; 
						}
					}
				} else {
					TAlert.show("删除机构失败","提示");
					return;
				}
			}
			
			/**
			 * 修改机构
			 * */
			protected function editLeafEventHandler(event:MouseEvent):void
			{
				var popwindow:AddOrgLeaf = AddOrgLeaf(PopUpManager.createPopUp(this, AddOrgLeaf, true));
				popwindow.owner = this;
				popwindow.parentOrg = orgTree.getParentItem(orgTree.selectedItem);
				popwindow.meOrg = orgTree.selectedItem;
				PopUpManager.centerPopUp(popwindow);
				popwindow.addEventListener("UpdateConfirmEvent", updateConfirmHandler);
			}
			
			protected function updateConfirmHandler(event:MouseEvent):void
			{
				if( windowObj.companyId == null ) {
					windowObj.companyId = getCompanyId(orgTree.selectedItem);
				}
				appCore.dataDeal.dataRemote("frameAPI","FrameAPI","updateOrg",[new JSONEncoder().encode(windowObj)]);
				appCore.dataDeal.remoteMethods.addEventListener(ResultEvent.RESULT, updateOrgHandler);
			}
			
			private function updateOrgHandler(event:ResultEvent):void
			{
				if(event.result != null && event.result.toString() != "0") {
					orgTree.selectedItem.orgName = windowObj.orgName;
					orgTree.selectedItem.orgCode = windowObj.orgCode;
					orgTree.selectedItem.orgType = windowObj.orgType;
				} else {
					TAlert.show("修改机构失败","提示");
					return;
				}
			}
			
			/**
			 * 添加子机构
			 * */
			protected function addLeafEventHandler(event:MouseEvent):void
			{
				var popwindow:AddOrgLeaf = AddOrgLeaf(PopUpManager.createPopUp(this, AddOrgLeaf, true));
				popwindow.owner = this;
				popwindow.parentOrg = orgTree.selectedItem;
				PopUpManager.centerPopUp(popwindow);
				popwindow.addEventListener("AddConfirmEvent",addConfirmHandler);
			}
			
			protected function addConfirmHandler(event:MouseEvent):void
			{
				if( windowObj.companyId == null ) {
					windowObj.companyId = getCompanyId(orgTree.selectedItem);
				}
				appCore.dataDeal.dataRemote("frameAPI","FrameAPI","addOrg",[new JSONEncoder().encode(windowObj)]);
				appCore.dataDeal.remoteMethods.addEventListener(ResultEvent.RESULT, addOrgHandler);
			}
			
			private function getCompanyId(childOrg:Object):int
			{
				var parentOrg:Object = orgTree.getParentItem(childOrg) as Object;
				if(parentOrg.orgType == 1) {
					return parentOrg.id;
				} else {
					return getCompanyId(parentOrg);
				}
			}
			
			private function addOrgHandler(event:ResultEvent):void
			{
				//新加的子结点
				var leaf:Object = new Object();
				if(event.result != null && event.result.toString() != "0") {
					leaf = new JSONDecoder().decode(event.result.toString());
				} else {
					TAlert.show("新建机构失败","提示");
					return;
				}
				
				//父结点
				var parent:Object = orgTree.selectedItem as Object; 
				if(parent.children != null) {
					parent.children.addItem(leaf);
				} else {
					parent.children = new ArrayCollection();
					parent.children.addItem(leaf);
				}
				
				//打开父节点，并选中新加的子节点
				orgTree.validateNow();
				orgTree.expandChildrenOf(parent, true);
				orgTree.selectedItem = leaf;
			}
			
			public function btn_import_clickHandler(event:MouseEvent):void
			{
				
			}
			
			public function btn_export_clickHandler(event:MouseEvent):void
			{
				
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	
	<s:VGroup width="100%" height="100%" 
			  paddingTop="10" paddingBottom="10" paddingLeft="10" paddingRight="10">
		<s:HGroup id="btn_hg"  width="100%" horizontalAlign="left" />
		<mx:Tree id="orgTree" width="100%" height="100%" dataProvider="{orgData}"
				 itemRenderer="com.itsm.common.renders.MenusTreeItemRender" 
				 labelField="orgName">
			
		</mx:Tree>
	</s:VGroup>
</s:Module>
